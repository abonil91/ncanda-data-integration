/*******************************************/
/* MARIJUANA DEPENDENCE &  ABUSE,  DSM3R    */
/*******************************************/
keep  mjdrdpdx mjdrdpa mjdrdpb
      mjdrabdx mjdrdpsx;


label mjdrdpdx='marijuana dependence, dsm3r';
label mjdrdpa='marijuana dependence, dsm3r criterion a';
label mjdrdpb='marijuana dependence, dsm3r criterion b';

label mjdrabdx='marijuana abuse, dsm3r';

label mjdrdpsx='num sx,dsm3r marijuana dependence';

mjdrdpdx=1;
/************************************/
/* MARIJUANA DEPENDENCE, CRITERION A */
/************************************/

mjdrdpa=1;
array mjdrdpa_(9) mjdrdpa1-mjdrdpa9;
do i=1 to 9;
   mjdrdpa_(i)=1;
end;

**** A1.Substance taken in larger amounts or over a longer period ***;
if (mj8=5) then mjdrdpa1=5;
else
   if (mj8 in(.K,.R)) then mjdrdpa1=9;

***  A2. Persistent desire or unsuccessful efforts to cut down or control **;
if (mj7=5) | (mj7a=5) then mjdrdpa2=5;
else
   if (mj7 in(.K,.R)) | (mj7a in(.K,.R)) then mjdrdpa2=9;

*** A3. A great deal of time spent ....;
if (mj5=5) then mjdrdpa3=5;
else
   if (mj5 in(.K,.R)) then mjdrdpa3=9;


*** A4. Frequent intoxication or withdrawal sx when expected to fulfill
      major obligations ...;
if (mj11a=5) | (mj11c=5) | (mj14=5) then mjdrdpa4=5;
else
   if (mj11a in(.K,.R)) | (mj11c in(.K,.R)) | (mj14 in(.K,.R)) then mjdrdpa4=9;

** A5. Important social, occupational, or recreational activities
       given up or reduced...;
if (mj17a=5) then mjdrdpa5=5;
else
   if (mj17a in(.K,.R)) then mjdrdpa5=9;

*** A6. Continued substance use despite .. social, psychological
        or physical problem ...;

   *** counters will be used later for Abuse (criteria A1,B);

cnt5dpa6=0;
cnt9dpa6=0;
array mjdpa6_ (7) mj6a1-mj6a5 mj12b mj13a;
do i=1 to 7;
  if mjdpa6_(i)=5 then cnt5dpa6+1;
  else
     if mjdpa6_(i) in(.K,.R) then cnt9dpa6+1;
end;
if cnt5dpa6>0 then mjdrdpa6=5;
else
   if cnt9dpa6>0 then mjdrdpa6=9;

*** A7.  Marked tolerance ......;
if (mj9=5) then mjdrdpa7=5;
else
   if (mj9 in(.K,.R)) then mjdrdpa7=9;


*** A8. Characteristic withdrawal sx;
if (mj10c=5) then mjdrdpa8=5;
else
   if (mj10c in(.K,.R)) then mjdrdpa8=9;



*** A9. Substance often taken to relieve or avoid withdrawal sx;
if (mj10b=5) then mjdrdpa9=5;
else
   if (mj10b in(.K,.R)) then mjdrdpa9=9;

    ****** count ******;
cnta5=0;
cnta9=0;
do i=1 to 9;
  if mjdrdpa_(i)=5 then cnta5+1;
  if mjdrdpa_(i) in(.K,.R) then cnta9+1;
end;
if cnta5>=3 then mjdrdpa=5;
else
   if sum(cnta5,cnta9)>=3 then mjdrdpa=9;

mjdrdpsx=cnta5;



 ********** DSMIIIR DEPENDENCE CRITERION B *******************;
**** persistence (at least 1 month) or recurrence;
mjdrdpb=1;
array mjdrdpb_ (7) mjdrdpa1 mjdrdpa3-mjdrdpa7 mjdrdpa9;




MJMnthCluster=(MJ5MnthCl=5)+
              ((MJ7MnthCl=5)|(MJ7aMnthCl=5))+
              (MJ8MnthCl=5)+
              (MJ9MnthCl=5)+
              (MJ10bMnthCl=5)+
              (MJ10cMnthCl=5)+ 
              ((MJ11aMnthCl=5)|(MJ11cMnthCl=5)|(MJ14MnthCl=5))+
              ((MJ6a1MnthCl=5)|(MJ6a3MnthCl=5)|
                (MJ6a4MnthCl=5)|(MJ6a5MnthCl=5)|(MJ12bMnthCl=5)|(MJ13aMnthCl=5))+
              (MJ17aMnthCl=5);



if MJMnthCluster>=3 then mjdrdpb=5;
else do;
   cnt5=0;
   cnt9=0;
   do i=1 to 7;
      if mjdrdpb_(i)=5 then cnt5+1;
      else
         if mjdrdpb_(i) in(.K,.R) then cnt9+1;
   end;
   if (mj10e>=3) | (mj10f>=30) then cnt5+1;
   else
      if (mj10e in(.K,.R)) | (mj10f in(.K,.R)) then cnt9+1;
   if (mj7=5) | (mj7b=5) then cnt5+1;
   else
      if (mj7 in(.K,.R)) | (mj7b in(.K,.R)) then cnt9+1;
   if cnt5>=3 then mjdrdpb=5;
   else 
      if (cnt5<3)*(sum(cnt5,cnt9)>=3)  then mjdrdpb=9;
end;

  ********** dependence: both criteria? ****************;
 if (mjdrdpa=5)*(mjdrdpb=5) then mjdrdpdx=5;
 else
    if (mjdrdpa in(5,9))*(mjdrdpb in(5,9)) then mjdrdpdx=9;
  ******************************************;


***********DSMIII-R MARIJUANA ABUSE *****************;
mjdrabdx=1;

**** A1 continued use despite knowledge of ...problems...;
mjdraba1=mjdrdpa6;

****A2 recurrent use in physically hazardous situations;
mjdraba2=1;
cnt5ab2= (mj11a=5) + (mj11c=5);
cnt9ab2= (mj11a in(.K,.R)) + (mj11c in(.K,.R));
if cnt5ab2>0 then mjdraba2=5;
else
   if cnt9ab2>0 then mjdraba2=9;



 ********** DSMIIIR ABUSE, CRITERION B *******************;
**** persistence (at least 1 month) or recurrence;
mjdrabb=1;
if sum(cnt5dpa6,cnt5ab2)>=2 then mjdrabb=5;
else
   if sum(cnt5dpa6,cnt5ab2,cnt9dpa6,cnt9ab2)>=2 then mjdrabb=9;


***** CRITERION C: has subject met criteria for dependence?;
if (mjdrdpdx=1) * ((mjdraba1=5)|(mjdraba2=5))*(mjdrabb=5) then mjdrabdx=5;
else
   if (mjdrdpdx in(1,9)) *((mjdraba1 in(5,9)) | (mjdraba2 in(5,9)))
      *(mjdrabb in(5,9)) then mjdrabdx=9;

********************************************************;
  *** set all variables to missing if section skipped;
array mjd3rarray (*) mjdrdpdx mjdrdpa mjdrdpb
      mjdrabdx mjdrdpsx;


if (mj1 in (.K,.R,.)) then do;
    do i=1 to dim(mjd3rarray);
        mjd3rarray(i)=.;
    end;
end;
*********************************************************;

